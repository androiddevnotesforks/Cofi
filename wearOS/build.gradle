plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.jaredsburrows.license'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    signingConfigs {
        if (keystoreProperties.containsKey('storeFile')) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    namespace 'com.omelan.cofi.wearos'
    compileSdk 34

    defaultConfig {
        applicationId "com.omelan.cofi"
        minSdk 25
        targetSdk 33
        versionCode 180
        versionName "1.12.8"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig keystoreProperties.containsKey('storeFile') ?
                    signingConfigs.release : signingConfigs.debug
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_19
        targetCompatibility JavaVersion.VERSION_19
    }
    kotlin {
        jvmToolchain(19)
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.8'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

licenseReport {
    generateCsvReport = false
    generateHtmlReport = false
    generateJsonReport = true
    // These options are ignored for Java projects
    copyHtmlReportToAssets = false
    copyHtmlReportToAssets = false
    copyJsonReportToAssets = true
}

dependencies {
    implementation project(path: ':cofi-share')
    implementation platform("androidx.compose:compose-bom:$compose_bom_version")

    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'com.google.android.gms:play-services-wearable:18.2.0'
    implementation "androidx.wear:wear-remote-interactions:1.0.0"
    implementation "androidx.wear.compose:compose-material:$wear_compose_version"
    implementation "androidx.wear.compose:compose-foundation:$wear_compose_version"
    implementation "androidx.wear.compose:compose-navigation:$wear_compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "com.google.android.horologist:horologist-compose-layout:0.5.20"
    implementation "com.google.accompanist:accompanist-pager:0.30.1"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.7.3'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.3'
    implementation 'androidx.activity:activity-compose:1.9.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.8.3"
    implementation "androidx.wear:wear-input:1.1.0"
    implementation 'androidx.wear:wear:1.3.0'

    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.6.8"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
    androidTestImplementation 'androidx.test:core:1.5.0'
}
